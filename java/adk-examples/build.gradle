plugins {
  id 'org.springframework.boot' version '3.5.4' apply false
  id 'io.spring.dependency-management' version '1.1.0' apply false
}

subprojects {
  apply plugin: 'org.springframework.boot'
  apply plugin: 'io.spring.dependency-management'
  apply plugin: 'java'
  apply plugin: 'java-library'
  apply plugin: 'jacoco'

  group = 'adk-examples'
  version = '0.0.0-SNAPSHOT'

  java {
    toolchain {
      languageVersion = JavaLanguageVersion.of(21)
    }
  }

  configurations {
    developmentOnly
    runtimeClasspath {
      extendsFrom developmentOnly
    }
    compileOnly {
      extendsFrom annotationProcessor
    }
  }

  repositories {
    mavenCentral()
  }

  dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'

    implementation 'com.google.adk:google-adk:0.2.0'
    implementation 'com.google.adk:google-adk-dev:0.2.0'

    // lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // test
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
      exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // mockito
    testRuntimeOnly 'org.mockito:mockito-core'
  }

  test {
    useJUnitPlatform()
  }

  jacocoTestReport {
    reports {
      html.required = true
      xml.required = false
      csv.required = false
    }
  }
}
